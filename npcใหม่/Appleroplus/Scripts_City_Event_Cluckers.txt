//==============================================
//==============================================
//==============================================
// - Script By : แม่มดน้อย จอมป่วน
// - Script City : Thailand 
// - Script Support Server : rAthena & Hercules
// Contacts
// Facebook https://www.facebook.com/appleroplus2
// Gmail appleroplus@gmail.com
// Skype appleroplus@hotmail.com
//==============================================
//==============================================
//==============================================
//= 1.3
//==============================================
prontera,147,281,4	script	ไก่หลอน	800,{

	set .@GMAccess,80;	// GM level required to access NPC

	if (.startcluck) {
		specialeffect2 EF_HIT3;
		switch(rand(15)) {
			case 0: npctalk "ไก่หลอน : กระต๊ากกกก!!!"; unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; emotion ET_HUK; break;
			case 1: npctalk "ไก่หลอน : กระต๊ากก!~"; break;
			case 2: unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; break;
			case 3: sc_start SC_Freeze,10000,0; break;
			case 4: npctalk "ไก่หลอน : กระต๊ากกกกกกกกกก!!!"; unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; emotion ET_HUK; break;
			case 5: sc_start SC_Sleep,10000,0; break;
			case 6: sc_start SC_Stone,10000,0; emotion ET_KIK; break;
			case 7: npctalk "ไก่หลอน : กระต๊ากกกกก!!!"; unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; emotion ET_HUK; break;
			case 8: npctalk "ไก่หลอน : กระต๊าก! กระต๊ากกกกก!!"; unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; emotion ET_HUK; break;
			case 9: sc_start SC_Stun,10000,0; break;
			case 10: sc_start SC_Sleep,10000,0; emotion ET_KIK; break;
			case 11: npctalk "ไก่หลอน : กระต๊าก! กระต๊าก!"; break;
			case 12: sc_start SC_Stun,10000,0; break;
			case 13: unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1; break;
			default:
				if (rand(50) < 4) {
					npctalk "ไก่หลอน : โน้ววววว!...........";
					specialeffect2 EF_SPHERE;
					announce "[กระต๊าก! กระต๊าก! บูมมม!] " + strcharinfo(0) + " คุณได้รับรางวัล! ทำได้ดี!",0;
					getitem $cluck_item_id,$cluck_item_amount;
					set .startcluck,0;
				} else {
					npctalk "ไก่หลอน : กระต๊าก! CLUUUCK!!";
					unitkill getcharid(3); skilleffect "NPC_SELFDESTRUCTION",1;
				}
				break;
		}
		end;
	}
	mes "[ไก่หลอน]";
	if (getgmlevel() >= .@GMAccess) {
		mes "กระต๊าก กระต๊าก! กระต๊ากกกกกก? ^FF0000~สวัสดี GM " + strcharinfo(0) + ", อยากเล่นวันนี้เหรอ?~^000000";
		mes "กระต๊าก กระต๊าก... กระต๊าก! ^FF0000~เพียงบอกฉันว่าจะทำอย่างไร!~^000000";
		next;
		while(1) switch(select("เริ่มเกม:ตรวจสอบรางวัล:เซ็ตค่ารางวัน:ไม่ใช่วันนี้ ไก่หลอน")) {
			case 1:
				mes "[ไก่หลอน]";
				mes "กระต๊าก! ^FF0000~แน่นอนค่ะ!~^000000";
				emotion ET_OK;
				close2;
				announce "[กระต๊าก! กระต๊าก! Boom!] กำลังจะเริ่มใน Prontera!",bc_blue;
				initnpctimer;
				end;
			case 2:
				mes "[ไก่หลอน]";
				mes "กระต๊าก, กระต๊าก กระต๊าก... กระต๊าก! ^FF0000~รางวัลปัจจุบันคือ^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +".^000000";
				next;
				break;
			case 3:
				mes "[ไก่หลอน]";
				mes "กระต๊าก กระต๊าก? กระต๊าก??? ^FF0000~รางวัลสำหรับการชนะควรเป็นอย่างไร? กรุณาใส่ ID~^000000";
				input $cluck_item_id;
				next;
				mes "[ไก่หลอน]";
				mes "กระต๊าก? กระต๊ากกกก? ^FF0000~ฉันควรแจกของชิ้นนี้เป็นจำนวนเท่าใด?~^000000";
				input $cluck_item_amount;
				next;
				mes "[ไก่หลอน]";
				mes "กระต๊าก กระต๊าก..? กระต๊าก. ^FF0000~ดังนั้นรางวัลคือ^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +"^000000? ^FF0000สินะ.~^000000";
				emotion ET_OK;
				next;
				break;
			case 4:
				mes "[ไก่หลอน]";
				mes "กระต๊าก กระต๊าก กระต๊าก...";
				close;
		}
	}
	mes "กระต๊าก กระต๊าก! กระต๊ากกกกก?";
	mes "กระต๊าก....";
	close;

OnTimer10000:
	announce "โปรดรีบไปทางน้ำพุถ้าคุณต้องการเล่นกับไก่บ้า!",bc_blue;
	end;
OnTimer20000:
	announce "ไก่หลอน ได้กินหนึ่งในรายการของฉัน! ฉันกลัวเหลือเกินกว่าจะเรียกคืนได้!",bc_blue;
	end;
OnTimer30000:
	announce "คลิกไก่บ้าและลองบีบไอเท็มออกหากคุณโชคดีคุณจะชนะ! คุณพร้อมไหม?",bc_blue;
	end;
OnTimer40000:
	announce "ไป! คลิกที่ไก่เพื่อรับรางวัล!",bc_blue;
	stopnpctimer;
	if (!$cluck_item_id) set $cluck_item_id,61001;
	if (!$cluck_item_amount) set $cluck_item_amount,30;
	set .startcluck,1;
//OnInit:
//Waitingroom "ไก่หลอน กดคลิกที่ไก่" , 0;
end;
}