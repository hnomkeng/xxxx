//==============================================
//==============================================
//==============================================
// - Script By : แม่มดน้อย จอมป่วน
// - Script City : Thailand 
// - Script Support Server : rAthena & Hercules
// Contacts
// Facebook https://www.facebook.com/appleroplus2
// Gmail appleroplus@gmail.com
// Skype appleroplus@hotmail.com
//==============================================
//==============================================
//==============================================
//= 1.2
//============================================== 
prontera,145,243,4	script	เพิ่มออฟชั่น Mora	10086,{
	if (checkweight(1201,1) == 0) {
		mes "โปรดลดน้ำหนักแล้วกลับมาหาเราใหม่นะค่ะ";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "โปรดลดน้ำหนักแล้วกลับมาหาเราใหม่นะค่ะ";
		close;
	}
	disable_items;
	mes "[เพิ่มออฟชั่น Mora]";
	mes "ยินดีที่รู้จักค่ะ";
	mes "ฉันอยู่ที่นี่ใน Mora ศึกษาโบราณสถานและสิ่งประดิษฐ์โบราณ การค้นพบใหม่ของ Adventurers ทำให้หมู่บ้านนี้น่าตื่นเต้นสำหรับฉัน";
	next;
	switch(select("บอกรายละเอียด:เสริมพลังออฟชั่นโมรา:ลบล้างการเสริมพลังออฟชั่น")) {
	case 1:
		mes "[เพิ่มออฟชั่น Mora]";
		mes "พบสิ่งประดิษฐ์ใหม่ ๆ ที่หมู่บ้านโมราแล้วคุณยังไม่เคยได้ยินข่าวนี้?";
		next;
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ถ้าคุณเจอสิ่งประดิษฐ์เหล่านี้มีพลังอำนาจที่ซ่อนอยู่ซึ่งสามารถสกัดออกมาได้";
		next;
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ฉันจะดำเนินการวิจัยต่อสิ่งประดิษฐ์เหล่านี้ต่อไป นักวิจัยของเรากำลังรับ Mora Coins เป็นเงินสำหรับการบริการของเรา";
		next;
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ฉันเสนอบริการของฉันเพื่อให้สามารถสร้างสิ่งประดิษฐ์ได้สำหรับ 100,000 Zeny และ 1 Mora Coin";
		next;
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ส่วนที่ดีที่สุดของบริการของฉันคือการปรับปรุงใด ๆ ที่คุณมีในสิ่งประดิษฐ์เหล่านี้จะไม่ได้รับอันตราย";
		close;
	case 2:
		set @mora_enchant_select,1;
		break;
	case 3:
		set @mora_enchant_select,2;
		break;
	}
	if (Zeny < 100000 || countitem(6380) == 0) {
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ฉันไม่ได้ให้บริการเหล่านี้สำหรับเพื่อนฟรี 100,000 Zeny และ 1 Mora Coin เป็นเรื่องที่มากเกินไปที่จะถาม?";
		close;
	}
	mes "[เพิ่มออฟชั่น Mora]";
	mes "โอเคร, คุณพร้อมหรือยัง? ก่อนที่ฉันจะเริ่มทำงานนี้คุณต้องการให้อุปกรณ์เสริมอะไร?";
	next;
	set .@i, select("ยกเลิก:1. อาวุธ:2. รองเท้า:3. ผ้าคลุม:4. ชุดเกาะ:5. เครื่องประดับ")-2;
	if (.@i == -1) {
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ตรวจสอบให้แน่ใจก่อนว่าคุณกำลังสวมอุปกรณ์อยู่";
		close;
	}
	setarray .@parts[0], EQI_HAND_R, EQI_SHOES, EQI_GARMENT, EQI_ARMOR, EQI_ACC_L;
	set .@part, .@parts[.@i];
	if (!getequipisequiped(.@part)) {
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ตรวจสอบให้แน่ใจก่อนว่าคุณกำลังสวมอุปกรณ์อยู่";
		close;
	}
	set .@equip_id, getequipid(.@part);
	set @mora_equip_part, .@part;

	//callsub L_Socket,<enchant type>,<bonus enchant type>,<4-x enchants possible>;
	// Enchant types:
	//   [1]ATK [2]CRIT [3]EVA [4]Healer [5]Spell 1 [6]Assist 1 [7]Assist 2 [8]STR [9]Range [10]Physical [11]Spell 2
	switch(.@part) {
	case EQI_HAND_R:
		switch(.@equip_id) {
			case 1660: callsub L_Socket,4,5,1;  //Wand_Of_Affection2
			case 2011: callsub L_Socket,5,11,1; //Golden_Rod_Staff2
			case 2012: callsub L_Socket,5,11,1; //Aqua_Staff2
			case 2013: callsub L_Socket,5,11,1; //Crimson_Staff2
			case 2014: callsub L_Socket,5,11,1; //Forest_Staff2
			case 16018: callsub L_Socket,1,5,1; //Mace_Of_Judgement2
		}
		break;
	case EQI_SHOES:
		switch(.@equip_id) {
			case 2475: callsub L_Socket,6,8,1;  //Ur_Greave
			case 2476: callsub L_Socket,6,10,1; //Peuz_Greave
			case 2477: callsub L_Socket,6,2,1;  //Sabah_Shoes
			case 2478: callsub L_Socket,6,1,1;  //Nab_Shoes
			case 2479: callsub L_Socket,6,2,1;  //White_Wing_Boots
			case 2480: callsub L_Socket,6,1,1;  //Black_Wing_Boots
		}
		break;
	case EQI_GARMENT:
		switch(.@equip_id) {
			case 2574: callsub L_Socket,3,8,1; //Ur_Manteau
			case 2575: callsub L_Socket,3,2,1; //Peuz_Manteau
			case 2577: callsub L_Socket,3,2,1; //Sabah_Hood
			case 2578: callsub L_Socket,3,2,1; //Nab_Hood
			case 2580: callsub L_Socket,3,1,1; //White_Wing_Manteau
			case 2581: callsub L_Socket,3,1,1; //Black_Wing_Manteau
		}
		break;
	case EQI_ARMOR:
		switch(.@equip_id) {
			case 15036: callsub L_Socket,8,1,1;  //Ur_Plate
			case 15037: callsub L_Socket,10,2,1; //Peuz_Plate
			case 15038: callsub L_Socket,2,10,1; //Sabah_Cloth
			case 15039: callsub L_Socket,1,2,1;  //Nab_Cloth
			case 15042: callsub L_Socket,2,9,1;  //White_Wing_Suits
			case 15043: callsub L_Socket,1,9,1;  //Black_Wing_Suits
		}
		break;
	case EQI_ACC_L:
		switch(.@equip_id) {
			case 2883: callsub L_Socket,6,0,2; //Ur_Seal
			case 2884: callsub L_Socket,6,0,2; //Peuz_Seal
			case 2886: callsub L_Socket,6,0,2; //Sabah_Ring
			case 2887: callsub L_Socket,6,0,2; //Nab_Ring
			case 2890: callsub L_Socket,6,0,2; //White_Wing_Brooch
			case 2891: callsub L_Socket,6,0,2; //Black_wing_Brooch
		}
		break;
	}
	mes "[เพิ่มออฟชั่น Mora]";
	mes "^990099"+getequipname(.@part)+"^000000??";
	mes "ฉันต้องการศึกษาอุปกรณ์นี้จริงๆ";
	set @mora_equip_part,0;
	close;

L_Socket:
	set .@select, @mora_enchant_select;
	set .@enchant_type, getarg(0);
	.@bonus_enchant = getarg(1);
	.@allowed_slot = getarg(2);
	set .@part, @mora_equip_part;
	set .@equip_id, getequipid(.@part);
	set .@equip_name$, getitemname(.@equip_id);
	set .@equip_refine, getequiprefinerycnt(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	set @mora_equip_part,0;
	set @mora_enchant_select,0;

	if (.@select == 1) {
		// If refine is +9 or higher, unlock bonus enchant type.
		if (.@equip_refine >= 9 && .@bonus_enchant > 0) {
			if (.@enchant_type < 1 || .@enchant_type > 11 || .@bonus_enchant > 11) {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "แหม๋มม บางสิ่งบางอย่างไม่ถูกต้อง";
				close;
			}
			setarray .@enchant_type$[1],"ATK Type","Critical Type","Evasion Type","Healer Type","Spell Ability 1","Assist Ability 1","Assist Ability 2","Strength","Range Type","Physical Type","Spell Ability 2";
			mes "[เพิ่มออฟชั่น Mora]";
			mes "เป็นการเสริมแรงให้กับอุปกรณ์ ในกรณีนี้จะได้รับการปรับปรุงเพิ่มเติม คุณต้องการเพิ่มสถิติใด?";
			next;
			switch(select(.@enchant_type$[.@enchant_type],.@enchant_type$[.@bonus_enchant])) {
			case 1:
				break;
			case 2:
				set .@enchant_type, .@bonus_enchant;
				break;
			}
		}
		if (.@equip_card[3] == 0 && .@allowed_slot < 4) {
			set .@slot,4;
			mes "[เพิ่มออฟชั่น Mora]";
			mes "พยายามที่จะเสริมสร้างความสามารถต่อไป การบังคับใช้ก่อนหน้านี้จะไม่ได้รับผลกระทบ คุณต้องการดำเนินการต่อหรือไม่?";
		} else if (.@equip_card[2] == 0 && .@allowed_slot < 3) {
			set .@slot,3;
			mes "[เพิ่มออฟชั่น Mora]";
			mes "กำลังพยายามเสริมอีกครั้ง การบังคับใช้ก่อนหน้านี้จะไม่ได้รับผลกระทบ";
			next;
			mes "[เพิ่มออฟชั่น Mora]";
			mes "ฉันอาจมีโอกาสที่จะล้มเหลวในขณะนี้ด้วยความพยายามนี้ คุณต้องการดำเนินการต่อหรือไม่?";
		} else if (.@equip_card[1] == 0 && .@allowed_slot < 2) {
			set .@slot,2;
			mes "[เพิ่มออฟชั่น Mora]";
			mes "เริ่มต้นการเพิ่มประสิทธิภาพครั้งที่สาม ฉันสามารถให้สถิติที่ดีที่สุดในขั้นตอนนี้ แต่จำไว้, ^990000ตอนนี้มีโอกาสสำหรับสิ่งประดิษฐ์ทีจะทำลายและการปรับปรุงก่อนหน้านีจะถูกลบ^000000. ดำเนินการต่อ?";
		} else {
			mes "[เพิ่มออฟชั่น Mora]";
			mes "สิ่งประดิษฐ์นี้ได้รับการเสริมสร้างความเข้มแข็งให้มีขีด จำกัด การเพิ่มประสิทธิภาพต่อไปเป็นไปไม่ได้ที่สถานะปัจจุบัน";
			close;
		}
		next;
		if(select("ฉันจะกลับมาในภายหลัง:ดำเนินการต่อ") == 1) {
			mes "[เพิ่มออฟชั่น Mora]";
			mes "ถ้าคุณเปลี่ยนใจกรุณากลับมา";
			close;
		}
		switch(.@enchant_type) {
		case 1: //Attack
			     if (.@slot == 4) set .@i, rand(1,525);
			else if (.@slot == 3) set .@i, rand(301,655);
			else if (.@slot == 2) set .@i, rand(451,750);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4700; //Strength1
			else if (.@i < 201) set .@enchant,4811; //Fighting_Spirit1
			else if (.@i < 301) set .@enchant,4819; //Atk1
			else if (.@i < 351) set .@enchant,4701; //Strength2
			else if (.@i < 401) set .@enchant,4810; //Fighting_Spirit2
			else if (.@i < 451) set .@enchant,4766; //Atk2
			else if (.@i < 476) set .@enchant,4702; //Strength3
			else if (.@i < 501) set .@enchant,4809; //Fighting_Spirit3
			else if (.@i < 526) set .@enchant,4767; //Atk3
			else if (.@i < 626) set .@enchant,0;
			else if (.@i < 641) set .@enchant,4703; //Strength4
			else if (.@i < 656) set .@enchant,4808; //Fighting_Spirit4
			else if (.@i < 661) set .@enchant,4704; //Strength5
			else if (.@i < 666) set .@enchant,4820; //Fighting_Spirit5
			else if (.@i < 668) set .@enchant,4705; //Strength6
			else if (.@i < 670) set .@enchant,4821; //Fighting_Spirit6
			else set .@enchant,9;
			break;
		case 2: //Critical
			     if (.@slot == 4) set .@i, rand(1,470);
			else if (.@slot == 3) set .@i, rand(201,610);
			else if (.@slot == 2) set .@i, rand(381,750);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4750; //Luck1
			else if (.@i < 201) set .@enchant,4700; //Strength1
			else if (.@i < 261) set .@enchant,4751; //Luck2
			else if (.@i < 321) set .@enchant,4701; //Strength2
			else if (.@i < 351) set .@enchant,4752; //Luck3
			else if (.@i < 381) set .@enchant,4702; //Strength3
			else if (.@i < 411) set .@enchant,4764; //Critical5
			else if (.@i < 441) set .@enchant,4818; //Sharp1
			else if (.@i < 471) set .@enchant,4752; //Luck3
			else if (.@i < 571) set .@enchant,0;
			else if (.@i < 586) set .@enchant,4753; //Luck4
			else if (.@i < 601) set .@enchant,4754; //Luck5
			else if (.@i < 606) set .@enchant,4765; //Critical7
			else if (.@i < 611) set .@enchant,4817; //Sharp2
			else if (.@i < 616) set .@enchant,4703; //Strength4
			else if (.@i < 618) set .@enchant,4816; //Sharp3
			else set .@enchant,9;
			break;
		case 3: //Evasion
			     if (.@slot == 4) set .@i, rand(1,525);
			else if (.@slot == 3) set .@i, rand(301,670);
			else if (.@slot == 2) set .@i, rand(451,800);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4859; //Evasion1
			else if (.@i < 201) set .@enchant,4750; //Luck1
			else if (.@i < 301) set .@enchant,4730; //Agility1
			else if (.@i < 351) set .@enchant,4860; //Evasion3
			else if (.@i < 401) set .@enchant,4751; //Luck2
			else if (.@i < 451) set .@enchant,4731; //Agility2
			else if (.@i < 476) set .@enchant,4731; //Agility2
			else if (.@i < 501) set .@enchant,4752; //Luck3
			else if (.@i < 526) set .@enchant,4732; //Agility3
			else if (.@i < 626) set .@enchant,0;
			else if (.@i < 641) set .@enchant,4762; //Evasion6
			else if (.@i < 656) set .@enchant,4753; //Luck4
			else if (.@i < 671) set .@enchant,4733; //Agility4
			else if (.@i < 676) set .@enchant,4763; //Evasion12
			else if (.@i < 681) set .@enchant,4754; //Luck5
			else if (.@i < 683) set .@enchant,4734; //Agility5
			else set .@enchant,9;
			break;
		case 4: //Healer
			     if (.@slot == 4) set .@i, rand(1,375);
			else if (.@slot == 3) set .@i, rand(201,535);
			else if (.@slot == 2) set .@i, rand(301,650);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4710; //Inteligence1
			else if (.@i < 201) set .@enchant,4720; //Dexterity1
			else if (.@i < 251) set .@enchant,4711; //Inteligence2
			else if (.@i < 301) set .@enchant,4721; //Dexterity2
			else if (.@i < 326) set .@enchant,4805; //Heal_Amount2
			else if (.@i < 351) set .@enchant,4712; //Inteligence3
			else if (.@i < 376) set .@enchant,4722; //Dexterity3
			else if (.@i < 476) set .@enchant,0;
			else if (.@i < 491) set .@enchant,4760; //Matk1
			else if (.@i < 506) set .@enchant,4850; //Heal_Amount3
			else if (.@i < 521) set .@enchant,4713; //Inteligence4
			else if (.@i < 536) set .@enchant,4723; //Dexterity4
			else if (.@i < 541) set .@enchant,4761; //Matk2
			else if (.@i < 546) set .@enchant,4851; //Heal_Amount4
			else if (.@i < 548) set .@enchant,4806; //Matk3
			else if (.@i < 550) set .@enchant,4852; //Heal_Amount5
			else set .@enchant,9;
			break;
		case 5: //Spell 1
			     if (.@slot == 4) set .@i, rand(1,600);
			else if (.@slot == 3) set .@i, rand(401,760);
			else if (.@slot == 2) set .@i, rand(401,766);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4710; //Inteligence1
			else if (.@i < 201) set .@enchant,4720; //Dexterity1
			else if (.@i < 301) set .@enchant,4795; //HP100
			else if (.@i < 401) set .@enchant,4815; //Spell1
			else if (.@i < 451) set .@enchant,4711; //Inteligence2
			else if (.@i < 501) set .@enchant,4721; //Dexterity2
			else if (.@i < 551) set .@enchant,4796; //HP200
			else if (.@i < 601) set .@enchant,4814; //Spell2
			else if (.@i < 701) set .@enchant,0;
			else if (.@i < 716) set .@enchant,4712; //Inteligence3
			else if (.@i < 731) set .@enchant,4722; //Dexterity3
			else if (.@i < 746) set .@enchant,4797; //HP300
			else if (.@i < 761) set .@enchant,4813; //Spell3
			else if (.@i < 763) set .@enchant,4713; //Inteligence4
			else if (.@i < 765) set .@enchant,4723; //Dexterity4
			else if (.@i < 767) set .@enchant,4812; //Spell4
			else set .@enchant,9;
			break;
		case 6: //Assist 1
			     if (.@slot == 4) set .@i, rand(1,520);
			else if (.@slot == 3) set .@i, rand(321,720);
			else if (.@slot == 2) set .@i, rand(521,850);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 81)  set .@enchant,4792; //Def6
			else if (.@i < 161) set .@enchant,4787; //Mdef4
			else if (.@i < 241) set .@enchant,4801; //SP100
			else if (.@i < 321) set .@enchant,4796; //HP200
			else if (.@i < 371) set .@enchant,4700; //Strength1
			else if (.@i < 421) set .@enchant,4720; //Dexterity1
			else if (.@i < 471) set .@enchant,4730; //Agility1
			else if (.@i < 521) set .@enchant,4740; //Vitality1
			else if (.@i < 621) set .@enchant,0;
			else if (.@i < 646) set .@enchant,4793; //Def9
			else if (.@i < 671) set .@enchant,4788; //Mdef6
			else if (.@i < 696) set .@enchant,4802; //SP150
			else if (.@i < 721) set .@enchant,4797; //HP300
			else if (.@i < 731) set .@enchant,4701; //Strength2
			else if (.@i < 741) set .@enchant,4721; //Dexterity2
			else if (.@i < 751) set .@enchant,4731; //Agility2
			else set .@enchant,9;
			break;
		case 7: //Assist 2
			     if (.@slot == 4) set .@i, rand(1,520);
			else if (.@slot == 3) set .@i, rand(321,720);
			else if (.@slot == 2) set .@i, rand(521,850);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 81)  set .@enchant,4792; //Def6
			else if (.@i < 161) set .@enchant,4787; //Mdef4
			else if (.@i < 241) set .@enchant,4801; //SP100
			else if (.@i < 321) set .@enchant,4796; //HP200
			else if (.@i < 371) set .@enchant,4710; //Inteligence1
			else if (.@i < 421) set .@enchant,4720; //Dexterity1
			else if (.@i < 471) set .@enchant,4730; //Agility1
			else if (.@i < 521) set .@enchant,4740; //Vitality1
			else if (.@i < 621) set .@enchant,0;
			else if (.@i < 646) set .@enchant,4793; //Def9
			else if (.@i < 671) set .@enchant,4788; //Mdef6
			else if (.@i < 696) set .@enchant,4802; //SP150
			else if (.@i < 721) set .@enchant,4797; //HP300
			else if (.@i < 731) set .@enchant,4711; //Inteligence2
			else if (.@i < 741) set .@enchant,4721; //Dexterity2
			else if (.@i < 751) set .@enchant,4741; //Vitality2
			else set .@enchant,9;
			break;
		case 8: //Strength
			     if (.@slot == 4) set .@i, rand(1,525);
			else if (.@slot == 3) set .@i, rand(301,685);
			else if (.@slot == 2) set .@i, rand(451,800);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4740; //Vitality1
			else if (.@i < 201) set .@enchant,4797; //HP300
			else if (.@i < 301) set .@enchant,4791; //Def3
			else if (.@i < 351) set .@enchant,4741; //Vitality2
			else if (.@i < 401) set .@enchant,4798; //HP400
			else if (.@i < 451) set .@enchant,4792; //Def6
			else if (.@i < 476) set .@enchant,4742; //Vitality3
			else if (.@i < 501) set .@enchant,4793; //Def9
			else if (.@i < 526) set .@enchant,4799; //HP500
			else if (.@i < 626) set .@enchant,0;
			else if (.@i < 641) set .@enchant,4742; //Vitality3
			else if (.@i < 656) set .@enchant,4743; //Vitality4
			else if (.@i < 671) set .@enchant,4794; //Def12
			//else if (.@i < 686) set .@enchant,01; // ??
			else if (.@i < 688) set .@enchant,4744; //Vitality5
			//else if (.@i < 690) set .@enchant,02; // ??
			else set .@enchant,9;
			break;
		case 9: //Range
			     if (.@slot == 4) set .@i, rand(1,470);
			else if (.@slot == 3) set .@i, rand(201,610);
			else if (.@slot == 2) set .@i, rand(321,750);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4750; //Luck1
			else if (.@i < 201) set .@enchant,4720; //Dexterity1
			else if (.@i < 261) set .@enchant,4751; //Luck2
			else if (.@i < 321) set .@enchant,4721; //Dexterity2
			else if (.@i < 351) set .@enchant,4752; //Luck3
			else if (.@i < 381) set .@enchant,4722; //Dexterity3
			else if (.@i < 411) set .@enchant,4764; //Critical5
			else if (.@i < 441) set .@enchant,4832; //Expert_Archer1
			else if (.@i < 471) set .@enchant,4753; //Luck4
			else if (.@i < 571) set .@enchant,0;
			else if (.@i < 586) set .@enchant,4723; //Dexterity4
			else if (.@i < 601) set .@enchant,4833; //Expert_Archer2
			else if (.@i < 606) set .@enchant,4765; //Critical7
			else if (.@i < 611) set .@enchant,4834; //Expert_Archer3
			else if (.@i < 616) set .@enchant,4724; //Dexterity5
			else if (.@i < 618) set .@enchant,4835; //Expert_Archer4
			else set .@enchant,9;
			break;
		case 10: //Physical
			     if (.@slot == 4) set .@i, rand(1,600);
			else if (.@slot == 3) set .@i, rand(401,800);
			else if (.@slot == 2) set .@i, rand(601,930);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4791; //Def3
			else if (.@i < 201) set .@enchant,4730; //Agility1
			else if (.@i < 301) set .@enchant,4750; //Luck1
			else if (.@i < 401) set .@enchant,4795; //HP100
			else if (.@i < 451) set .@enchant,4792; //Def6
			else if (.@i < 501) set .@enchant,4731; //Agility2
			else if (.@i < 551) set .@enchant,4751; //Luck2
			else if (.@i < 601) set .@enchant,4796; //HP200
			else if (.@i < 701) set .@enchant,0;
			else if (.@i < 726) set .@enchant,4793; //Def9
			else if (.@i < 751) set .@enchant,4732; //Agility3
			else if (.@i < 776) set .@enchant,4752; //Luck3
			else if (.@i < 801) set .@enchant,4797; //HP300
			else if (.@i < 816) set .@enchant,4733; //Agility4
			else if (.@i < 831) set .@enchant,4753; //Luck4
			else if (.@i < 833) set .@enchant,4807; //Atk_Speed1
			else set .@enchant,9;
			break;
		case 11: //Spell 2
			     if (.@slot == 4) set .@i, rand(1,600);
			else if (.@slot == 3) set .@i, rand(401,760);
			else if (.@slot == 2) set .@i, rand(401,766);
			else {
				mes "[เพิ่มออฟชั่น Mora]";
				mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น";
				close;
			}
			     if (.@i < 101) set .@enchant,4711; //Inteligence2
			else if (.@i < 201) set .@enchant,4721; //Dexterity2
			else if (.@i < 301) set .@enchant,4796; //HP200
			else if (.@i < 401) set .@enchant,4814; //Spell2
			else if (.@i < 451) set .@enchant,4712; //Inteligence3
			else if (.@i < 501) set .@enchant,4722; //Dexterity3
			else if (.@i < 551) set .@enchant,4760; //Matk1
			else if (.@i < 601) set .@enchant,4813; //Spell3
			else if (.@i < 701) set .@enchant,0;
			else if (.@i < 716) set .@enchant,4713; //Inteligence4
			else if (.@i < 731) set .@enchant,4723; //Dexterity4
			else if (.@i < 746) set .@enchant,4761; //Matk2
			else if (.@i < 761) set .@enchant,4812; //Spell4
			else if (.@i < 763) set .@enchant,4714; //Inteligence5
			else if (.@i < 765) set .@enchant,4724; //Dexterity5
			else if (.@i < 767) set .@enchant,4806; //Matk3
			else set .@enchant,9;
			break;
		default:
			mes "[เพิ่มออฟชั่น Mora]";
			mes "ฉันคิดว่าฉันผิด...";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@equip_refine) ||
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		if (.@equip_card[3] == 0 && .@allowed_slot < 4) {
			set .@equip_card[3],.@enchant;
		} else if (.@equip_card[2] == 0 && .@allowed_slot < 3) {
			set .@equip_card[2],.@enchant;
			if (.@enchant == 0) {
				set .@equip_card[3],0;
			}
		} else if (.@equip_card[1] == 0 && .@allowed_slot < 2) {
			set .@equip_card[1],.@enchant;
			if (.@enchant == 0) {
				set .@equip_card[2],0;
				set .@equip_card[3],0;
			}
		} else {
			mes "[เพิ่มออฟชั่น Mora]";
			mes "ฉันคิดว่ามีข้อ จำกัด ในการสร้างสิ่งประดิษฐ์ขึ้น";
			close;
		}
		if (Zeny < 100000 || countitem(6380) == 0) {
			mes "[เพิ่มออฟชั่น Mora]";
			mes "ฉันจะไม่ทำงานจนกว่าคุณจะเสียค่าธรรมเนียม";
			close;
		}
		delitem 6380,1; //Mora_Coin
		set Zeny, Zeny-100000;
		delequip .@part;
		if (.@enchant == 9) {
			specialeffect2 EF_SUI_EXPLOSION;
			mes "[เพิ่มออฟชั่น Mora]";
			mes "นี้ๆ";
 			mes "ฉันขอโทษ แต่อุปกรณ์ถูกทำลาย";
			close;
		}
		if (.@enchant == 0) {
			specialeffect2 EF_SHIELDCHARGE;
			mes "[เพิ่มออฟชั่น Mora]";
			mes "เนื่องจากความสมดุลของพลังงานไม่ตรงกันความสามารถในการระบุที่มีอยู่จะถูกทำลาย เป็นโชคร้าย แต่โปรดลองอีกครั้งในครั้งต่อไป";
		} else {
			specialeffect2 EF_REPAIRWEAPON;
			mes "[เพิ่มออฟชั่น Mora]";
			// Replacing original dialogue since it doesn't make sense.
			//mes "The artifact ^990000"+.@slot+" will have the first enhancement retained.";
			mes "ฉันได้ปรับปรุง artifact เรียบร้อยแล้ว ^990000"+.@equip_name$+"^000000 ที่ซ็อกเก็ต "+.@slot+".";
		}

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		if (Zeny < 100000 || countitem(6380) == 0) {
			mes "[เพิ่มออฟชั่น Mora]";
			mes "คุณต้องจ่ายเงินหากต้องการรับบริการ";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[เพิ่มออฟชั่น Mora]";
		mes "ลบล้างความสามารถในการเพิ่มส่วนที่เหลือ";
		delitem 6380,1; //Mora_Coin
		set Zeny, Zeny-100000;
		delequip .@part;

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	}
end;
//OnInit:
//waitingroom "เพิ่มออฟชั่น Mora",0;
//end;
}
