//==============================================
//==============================================
//==============================================
// - Script By : แม่มดน้อย จอมป่วน
// - Script City : Thailand 
// - Script Support Server : rAthena & Hercules
// Contacts
// Facebook https://www.facebook.com/appleroplus2
// Gmail appleroplus@gmail.com
// Skype appleroplus@hotmail.com
//==============================================
//==============================================
//==============================================
prontera,167,227,4	script	เควสการ์ดแลกแต้มสะสม	743,{

	mes "[เควสการ์ดแลกแต้มสะสม]";
	mes "สวัสดี, "+strcharinfo(0)+"!";
	mes "มีอะไรให้ฉันช่วยคุณได้บ้าง ?";
	next;
	switch(select("^FF0000^000000 รายละเอียดข้อมูล:^FF0000^000000 ขายการ์ด :^FF0000^000000 แลกเปลี่ยนสินค้า (^0055FF"+getd(.Points$)+"^000000):^FF0000^000000 ยกเลิก")) {
	case 1:
		mes "[รายละเอียดข้อมูล]";
		mes "ถ้าหากคุณมีไอเท็มประเภทการ์ด";
		mes "ที่ไม่ได้ใช้ หรือ ไม่มีค่าสำหรับคุณ?";
		mes "คุณสามารถที่จะขายให้ฉันได้";
		mes "และ ฉันก๊จะมีสิ่งของตอบแทนสำหรับสิ่งของเหล่านั้น!";
		next;
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "ฉันจะให้ ^0055FF"+.Points[0]+" Point"+((.Points[0] == 1)?"":"")+"^000000 ต่อ การ์ด";
		mes "ปกติ ของคุณ , และ";
		mes "^0055FF"+.Points[1]+" Points^000000 สำหรับการ์ดบอส.";
		mes "และ คุณสามารถนำ Point มาแลกเปลี่ยนสิ่งของ";
		mes "กับ ฉันในภายหลังได้.";
		mes "คุณต้องการที่จะใช้บริการฉันเลยใหม ?";
		emotion ET_MONEY;
		close;
	case 2:
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "กรุณาเลือการ์ดของคุณ";
		mes "ที่จะต้องการแลกเปลี่ยน.";
		deletearray @sold_nameid[0],getarraysize(@sold_nameid);
		callshop "Point_Shops",2;
		npcshopattach "Point_Shops";
		end;
	case 3:
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "คุณมี ^0055FF"+getd(.Points$)+"^000000 Point"+((getd(.Points$) == 1)?".":"s.");
		callshop "Point_Shops",1;
		npcshopattach "Point_Shops";
		end;
	case 4:
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "ไว้พบกันใหม่!";
		emotion ET_SLEEPY;
		close;		
	}

OnSellItem:
	
	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)
		if (@sold_nameid[.@i] > 4000 && @sold_nameid[.@i] < 4700)
		{
			set .@card_id[getarraysize(.@card_id)], @sold_nameid[.@i];
			set .@card_amt[getarraysize(.@card_amt)], @sold_quantity[.@i];
			set .@mvp, compare(.MvPCards$,""+@sold_nameid[.@i]);
			mes ((.@mvp)?"  ^FF0000":"  ^777777")+getitemname(@sold_nameid[.@i])+" จำนวน "+@sold_quantity[.@i]+" ใบ^000000";
			set .@card_total, .@card_total+(@sold_quantity[.@i]*((.@mvp)?.Points[1]:.Points[0]));
		}
	deletearray @sold_nameid[0], getarraysize(@sold_nameid);
	deletearray @sold_quantity[0], getarraysize(@sold_quantity);
	if (!.@card_id)
	{
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "รายการไอเท็มที่แลกเปลี่ยนมันไม่ใช่การ์ด";
		mes "กรุณาทำรายการใหม่อีกครั้ง";
		emotion ET_SWEAT;
		close;
	}
	mes " ";
	mes "ราคาทั้งหมด คือ : ^0055FF"+.@card_total+" Point.^000000";
	mes "คุณแน่ใจที่จะต้องการขาย ?";
	next;
	if(select("^FF0000^000000ใช่ ข้าต้องการขายทันที:^FF0000^000000ข้าขอ... คิดใหม่อีกครั้งดีกว่า") == 2)
	{
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "อื้มมม, โอเค ไว้ต้องการเมื่อไรค่อยกลับมาอีกครั้ง...";
		emotion ET_SCRATCH;
		close;
	}
	for(set .@i,0; .@i<getarraysize(.@card_id); set .@i,.@i+1)
		delitem .@card_id[.@i],.@card_amt[.@i];
	setd .Points$, getd(.Points$)+.@card_total;
	mes "[เควสการ์ดแลกแต้มสะสม]";
	mes "แลกเปลี่ยนสำเร็จ !";
	mes "ขอบคุณสำหรับการซื้อขาย แลกเปลี่ยน";
	emotion ET_DELIGHT;
	close;

OnBuyItem:
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j])
			{
				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	if (.@cost > getd(.Points$))
	{
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "คุณมีจำนวน Points ไม่เพียงพอต่อการแลกเปลี่ยน.";
		emotion ET_HUK;
	}
	else {
		mes "[เควสการ์ดแลกแต้มสะสม]";
		mes "	";
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		{
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			mes "  ^777777"+getitemname(@bought_nameid[.@i])+" จำนวน "+@bought_quantity[.@i]+" อัน^000000";
		}
		mes " ";
		mes "ราคาทั้งหมด คือ : ^0055FF"+.@cost+" pt.^000000";
		setd .Points$, getd(.Points$)-.@cost;
		emotion ET_MONEY;
	}
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnInit:

	set .Points$,"#Card_Points";	// ค่าตัวแปร เก็บคะแนนพอยท์ (ห้ามแก้ใขมีผลต่อ Base ที่เก็บค่าบน SQL)
	setarray .Shop[0],61001,20,
					     13571,500,// ตั้งค่า เพิ่ม / ลบ / แก้ใข รายการไอเท็มที่ใช้การซื้อขาย : <รหัสไอเท็ม>,<จำนวนราคา>
					  6231,400,
					  6235,400,
					  12412,350,
					  7227,5,
					  7539,1;
					  
	setarray .Points[0],50,1000;		// ตั้งค่า ราคาการขายการ์ดให้กับ Npc <การ์ดปกติ>,<การ์ด บอส>
	set .MvPCards$,					// ตั้งค่า รหัสไอเท็ม การ์ด สำหรับการ์ดบอส. (ราคาปัจจุบันสำหรับการ์ดบอส ต่อ 1 ใบ คือ 300 ที่จะได้รับเมื่อขาย)
	  "4121,4123,4128,4131,4132,4134,4135,4137,4143,4144,4145,4146,4147,4148,4168,4236,"+
	  "4241,4263,4276,4302,4305,4318,4324,4330,4342,4357,4359,4361,4363,4365,4399,4403,4407,4142";

	npcshopdelitem "Point_Shops",909;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "Point_Shops",.Shop[.@i],.Shop[.@i+1];
//	waitingroom "เควสการ์ดแลกแต้มสะสม",0; 
	end;
}

-	shop	Point_Shops	-1,909:-1