//==============================================
//==============================================
//==============================================
// - Script By : แม่มดน้อย จอมป่วน
// - Script City : Thailand 
// - Script Support Server : rAthena & Hercules
// Contacts
// Facebook https://www.facebook.com/appleroplus2
// Gmail appleroplus@gmail.com
// Skype appleroplus@hotmail.com
//==============================================
//==============================================
//==============================================
//= 1.1a
//==============================================
prontera,144,227,4	script	ใส่ชื่อลงบน Item	47,{
	
	setarray .@Item[0],61001,20;	// ความต้องการของรายการ: <ID>,<Count>{,...} (0 to disable)
	setarray .@Cost[0],0,100000;	// ข้อกำหนดของ Zeny: <base price>,<price per refine>

	disable_items;
	mes "[ใส่ชื่อลงบน Item]";
	mes "สวัสดี ฉันสามารถใส่ชื่อของคุณ ลงใน Item ที่คุณต้องการได้";
	next;
	if(select("^FF0000^000000 สอบถามรายละเอียด:^FF0000^000000 ต้องการใช้บริการ") == 1) {
		mes "[ใส่ชื่อลงบน Item]";
		mes "สวัสดี ฉันสามารถใส่ชื่อของคุณ ลงใน Item ที่คุณต้องการได้ โดยชื่อของคุณจะถูกสลักใส่ไว้ใน Item ชิ้นนั้นตลอดไป";
		emotion ET_AHA;
		next;
		mes "[ใส่ชื่อลงบน Item]";
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "สำหรับงานของฉัน ฉันต้องการ:";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes " ~ "+callfunc("F_InsertComma",.@Cost[0])+" เซนี";
			if (.@Cost[1]) mes " ~ "+callfunc("F_InsertComma",.@Cost[1])+" เซนีต่อการตีบวก";
		}
		else mes "บริการนี้ไม่ฟรี";
		next;
		emotion ET_CRY;
		mes "[ใส่ชื่อลงบน Item]";
		mes "ขอบคุณ";
		emotion ET_SCRATCH;
		close;
	}
	mes "[ใส่ชื่อลงบน Item]";
	mes "คุณต้องการ ใส่ชื่อลงใน Item ชิ้นใด";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[ใส่ชื่อลงบน Item]";
	if (!getequipisequiped(.@part)) {
		mes "คุณยังไม่ได้ติดตั้ง Item ในตัวละคร";
		emotion ET_KEK;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "ไม่สามารถทำงานได้เนื่องจาก รายการนี้ได้ลงนามแล้ว";
		emotion ET_SCRATCH;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "ไม่สามารถ สลักชื่อลงใน Item ที่มีการ์ดได้";
		emotion ET_SCRATCH;
		close;
	}
	if (getiteminfo(.@id,10)) {
		mes "ไม่สามารถ สลักชื่อลงใน Item ที่มีรูได้";
		emotion ET_SORRY;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "ไม่สามารถ สลักชื่อลงใน Item เช่าได้";
			emotion ET_SCRATCH;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "ฉันต้องการ:";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" เซนี";
	}
	mes "ฉันจะลงนามของคุณ ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("^FF0000^000000 ตกลง:^FF0000^000000 ยกเลิก")==2) {
		mes "[ใส่ชื่อลงบน Item]";
		mes "แล้วพบกันใหม่...";
		emotion ET_SLEEPY;
		close;
	}
	mes "[ใส่ชื่อลงบน Item]";
	if (Zeny < .@price || .@nr) {
		mes "ขอบคุณ'";
		emotion ET_SORRY;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Where is "+getitemname(@id)+"...?";
		npctalk "ใส่ชื่อลงบน Item : คุณเป็นนักต้มตุ๋นที่งี่เง่า!";
		logmes "คุณพยายามลงชื่อรายการที่ไม่มี: "+getitemname(@id);
		emotion ET_KEK;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Done!";
	emotion ET_OK;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	end;
//	OnInit:
//	Waitingroom "ใส่ชื่อลงบน Item" , 0;
//	end;
	}
