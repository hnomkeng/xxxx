//==============================================
//==============================================
//==============================================
// - Script By : แม่มดน้อย จอมป่วน
// - Script City : Thailand 
// - Script Support Server : rAthena & Hercules
// Contacts
// Facebook https://www.facebook.com/appleroplus2
// Gmail appleroplus@gmail.com
// Skype appleroplus@hotmail.com
//==============================================
//==============================================
//==============================================
//= 1.4
//==============================================
prontera,165,229,4	script	ภารกิจนักล่ามอนเตอร์	556,{
function Chk;
	mes "[ภารกิจนักล่ามอนเตอร์]";
	mes "สวัสดี, " + strcharinfo(0) + "!";
	if (!#Mission_Delay) {
		next;
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "ฉันยังไม่พบการลงทะเบียนใดๆเลย";
		mes "คุณเป็นนักล่าหน้าใหม่สินะ!";
		emotion ET_HUK;
		next;
		callsub Mission_Info;
		emotion ET_GO;
		#Mission_Delay = 1;
		close;
	}
	mes F_Rand("ทำงานหนักเช่นเคย...", "ฉันหวังว่าจะไม่หย่อนยาน...");
	mes "มีอะไรที่ฉันสามารถช่วยได้";
	mes "บ้างไหม?";
	mes " ";
	mes "^777777^FF0000^000000 คุณทำเสร็จแล้ว ^FF0000" + F_InsertPlural(Mission_Total,"ภารกิจ",0,"^0055FF%d^777777 %s") + ".^000000 ^FF0000^000000";
	next;
	switch(select(
		((!Mission0) ? "^FF0000^000000 ภารกิจใหม่::" : ":^FF0000^000000 สถานะภารกิจ:^FF0000^000000 ละทิ้งภารกิจ") +
		":^FF0000^000000 ข้อมูล:^FF0000^000000 ร้านค้าภารกิจ:^FF0000^000000 ดู อันดับนักล่า ยอดนิยม:^FF0000^000000 ^777777ยกเลิก^000000"
	)) {
	case 1:
		mes "[ภารกิจนักล่ามอนเตอร์]";
		if (#Mission_Count) {
			mes "เจ้าได้เริ่มภารกิจ";
			mes "กับตัวละครอื่น.";
			if (!@hm_char_del_check) {  // ตรวจสอบอักขระที่ถูกลบ
				query_sql("SELECT 1 FROM `char_reg_num` WHERE `key` = 'Mission0' AND `char_id` IN(SELECT `char_id` FROM `char` WHERE `account_id` = " + getcharid(3) + ")", .@i);
				if (!.@i) {
					next;
					mes "[ภารกิจนักล่ามอนเตอร์]";
					mes "ฉันหาการลงทะเบียนไม่พบ";
					mes "สำหรับตัวละครนั้นแม้ว่า...";
					mes "โปรดรอสักครู่.";
					emotion ET_SCRATCH;
					#Mission_Count = 0;
				}
				@hm_char_del_check = true;
			}
			close;
		}
		if (#Mission_Delay > gettimetick(2) && .Delay) {
			mes "ฉันเกรงว่าคุณจะต้องรอ" + Time2Str(#Mission_Delay) + " ก่อนที่จะทำภารกิจอื่น.";
			close;
		}
		mes "คุณต้องตามล่า:";
		query_sql("SELECT ID FROM `" + .mob_db$ + "` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 0 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT " + .Quests, .@mob);
		for (.@i = 0; .@i < .Quests; .@i++) {
			setd "ภารกิจ" + .@i, .@mob[.@i];
			setd "ภารกิจ" + .@i +"_", 0;
		}
		#Mission_Count = rand(.Count[0], .Count[1]);
		callsub Mission_Status;
		next;
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "รายงานกลับเมื่อ";
		mes "คุณทำเสร็จแล้ว";
		mes "โชคดี!";
		close;
	case 2:
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "สถานะภารกิจ:";
		callsub Mission_Status;
		close;
	case 3:
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "คุณต้องการที่จะ";
		mes "ละทิ้งภารกิจของคุณ?";
		if (.Reset < 0 && .Delay)
			mes "เวลาหน่วงของคุณจะไม่ถูกรีเซ็ต.";
		else if (.Reset > 0)
			mes "มันจะมีค่าใช้จ่าย " + F_InsertComma(.Reset) + " Zeny.";
		next;
		switch(select("^FF0000^000000 ละทิ้ง...: ^FF0000^000000 ^777777ยกเลิก^000000")) {
		case 1:
			if (.Reset > 0) {
				if (Zeny < .Reset) {
					mes "[ภารกิจนักล่ามอนเตอร์]";
					mes "คุณมีไม่เพียงพอ";
					mes "ต้องใช้ Zeny สำหรับ ภารกิจนี้.";
					emotion ET_SORRY;
					close;
				}
				Zeny -= .Reset;
				emotion ET_MONEY;
			}
			mes "[ภารกิจนักล่ามอนเตอร์]";
			mes "เอาล่ะฉันตกไปแล้ว";
			mes "ภารกิจปัจจุบันของคุณ.";
			specialeffect2 EF_STORMKICK4;
			for (.@i = 0; .@i < .Quests; .@i++) {
				setd "ภารกิจ"+.@i, 0;
				setd "ภารกิจ"+.@i+"_", 0;
			}
			#Mission_Count = 0;
			if (.Reset < 0 && .Delay)
				#Mission_Delay = gettimetick(2) + (.Delay * 3600);
			close;
		case 2:
			mes "[ภารกิจนักล่ามอนเตอร์]";
			mes "ฉันรู้ว่าคุณล้อเล่น!";
			mes "ติดตามการทำงานที่ดี.";
			emotion ET_SMILE;
			close;
		}
	case 4:
		callsub Mission_Info;
		close;
	case 5:
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "คุณมี ^0055FF" + #Mission_Points + "^000000 คะแนนภารกิจ.";
		mes "ใช้มันให้ดี!";
		callshop "mission_shop",1;
		npcshopattach "mission_shop";
		end;
	case 6:
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "นักล่าชั้นนำคือ:";
		query_sql("SELECT char_id AS id, (SELECT `name` FROM `char` WHERE char_id = id),`value` FROM `char_reg_num` WHERE `key` = 'Mission_Total' ORDER BY CAST(`value` AS SIGNED) DESC LIMIT 5", .@id, .@name$, .@val);
		for (.@i = 0; .@i < 5; .@i++)
			mes "  [Rank " + (.@i+1) + "]  " + ((.@name$[.@i] == "") ? "^777777none" : "^0055FF" + .@name$[.@i]+"^000000 : ^FF0000" + .@val[.@i] + " pt.") + "^000000";
		close;
	case 7:
		mes "[ภารกิจนักล่ามอนเตอร์]";
		mes "ไม่มีอะไร? ถูก...";
		emotion ET_SCRATCH;
		close;
	}
	end;

Mission_Status:
	@f = false;
	deletearray .@j[0], getarraysize(.@j);
	for (.@i = 0; .@i < .Quests; .@i++) {
		.@j[.@i] = getd("ภารกิจ" + .@i);
		.@j[.Quests] = .@j[.Quests] + strmobinfo(3,.@j[.@i]);
		.@j[.Quests+1] = .@j[.Quests+1] + (strmobinfo(6,.@j[.@i]) / (getbattleflag("base_exp_rate") / 100) * .Modifier[0]);
		.@j[.Quests+2] = .@j[.Quests+2] + (strmobinfo(7,.@j[.@i]) / (getbattleflag("job_exp_rate") / 100) * .Modifier[1]);
		mes " > "+Chk(getd("ภารกิจ"+.@i+"_"),#Mission_Count) + strmobinfo(1,.@j[.@i]) + " (" + getd("ภารกิจ"+.@i+"_") + "/" + #Mission_Count + ")^000000";
	}

	// Reward formulas:
	.@Mission_Points = 3 + (.@j[.Quests] / .Quests / 6);
	.@Base_Exp = #Mission_Count * .@j[.Quests+1] / 5;
	.@Job_Exp = #Mission_Count * .@j[.Quests+2] / 5;
	.@Zeny = #Mission_Count * .Quests * .@j[.@i] * .Modifier[2];

	next;
	mes "[ภารกิจนักล่ามอนเตอร์]";
	mes "ภารกิจของรางวัล:";
	mes " > คะแนนภารกิจ: ^0055FF" + .@Mission_Points + "^000000";
	mes " > ประสบการณ์พื้นฐาน: ^0055FF" + F_InsertComma(.@Base_Exp) + "^000000";
	mes " > ประสบการณ์การทำงาน: ^0055FF" + F_InsertComma(.@Job_Exp) + "^000000";
	mes " > Zeny: ^0055FF" + F_InsertComma(.@Zeny) + "^000000";
	if (@f) {
		@f = false;
		return;
	}
	next;
	mes "[ภารกิจนักล่ามอนเตอร์]";
	mes "โอ้เสร็จแล้ว!";
	mes "ทำได้ดีมาก.";
	mes "นี่คือรางวัลของคุณ.";
	emotion ET_BEST;
	specialeffect2 EF_ANGEL;
	specialeffect2 EF_TRUESIGHT;
	#Mission_Points += .@Mission_Points;
	BaseExp += .@Base_Exp;
	JobExp += .@Job_Exp;
	Zeny += .@Zeny;
	for (.@i = 0; .@i < .Quests; .@i++) {
		setd "ภารกิจ" + .@i, 0;
		setd "ภารกิจ" + .@i+"_", 0;
	}
	#Mission_Count = 0;
	if (.Delay)
		#Mission_Delay = gettimetick(2) + (.Delay * 3600);
	Mission_Total++;
	if (Mission_Total == 1)
		query_sql("INSERT INTO `char_reg_num` (`char_id`,`key`,`index`,`value`) VALUES (" + getcharid(0) + ",'Mission_Total','0',1)");
	else
		query_sql("UPDATE `char_reg_num` SET `value` = " + Mission_Total + " WHERE `char_id` = " + getcharid(0) + " AND `key` = 'Mission_Total'");
	close;

Mission_Info:
	mes "[ภารกิจนักล่ามอนเตอร์]";
	mes "ถ้าคุณเลือกฉันสามารถมอบหมาย";
	mes "เจ้าเป็นเควสแบบสุ่ม.";
	mes "บางคนง่ายกว่าคนอื่น ๆ แต่";
	mes "รางวัลเพิ่มขึ้นด้วยความยากลำบาก.";
	next;
	mes "[ภารกิจนักล่ามอนเตอร์]";
	mes "มีการแบ่งปันคะแนนภารกิจ";
	mes "ท่ามกลางตัวละครทั้งหมดของคุณ.";
	if (.Delay)
		mes "เวลาล่าช้าก็เช่นกัน.";
	mes "คุณไม่สามารถทำภารกิจได้";
	mes "หลายตัวพร้อมกัน.";
	next;
	mes "[ภารกิจนักล่ามอนเตอร์]";
	mes "คุณสามารถเริ่มภารกิจ";
	mes (.Delay ? "ทุกๆ " + ((.Delay == 1) ? "ชั่วโมง." : .Delay + " ชั่วโมง.") : "เมื่อไหร่ก็ได้ที่คุณต้องการ.");
	mes "นั่นคือทุกอย่าง~";
	return;

function Chk {
	if (getarg(0) < getarg(1)) {
		@f = true;
		return "^FF0000";
	} else
		return "^00FF00";
}

OnBuyItem:
	.@size = getarraysize(@bought_nameid);
	for (.@i = 0; .@i < .@size; .@i++) {
		.@j = inarray(.Shop, @bought_nameid[.@i]);
		.@cost += (.Shop[.@j+1] * @bought_quantity[.@i]);
	}
	mes "[ภารกิจนักล่ามอนเตอร์]";
	if (.@cost > #Mission_Points)
		mes "คุณมีคะแนนภารกิจไม่เพียงพอ.";
	else {
		for (.@i = 0; .@i < .@size; .@i++) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			dispbottom "Purchased " + @bought_quantity[.@i] + "x " + getitemname(@bought_nameid[.@i]) + ".";
		}
		#Mission_Points -= .@cost;
		mes "ข้อตกลงเสร็จสมบูรณ์";
		emotion ET_MONEY;
	}
	deletearray @bought_nameid[0], .@size;
	deletearray @bought_quantity[0], .@size;
	close;

OnNPCKillEvent:
	if (!getcharid(1) || !.Party) {
		if (!#Mission_Count || !Mission0) end;
		for (.@i = 0; .@i < .Quests; .@i++) {
			if (strmobinfo(1,killedrid) == strmobinfo(1,getd("ภารกิจ" + .@i))) {
				if (getd("ภารกิจ" + .@i + "_") < #Mission_Count) {
					dispbottom "[ภารกิจนักล่ามอนเตอร์] Killed " + (set(getd("ภารกิจ" + .@i + "_"),getd("ภารกิจ" + .@i + "_") + 1)) +
					           " of " + #Mission_Count + " " + strmobinfo(1,killedrid) + ".";
					end;
				}
			}
		}
	} else if (.Party) {
		.@mob = killedrid;
		getmapxy(.@map1$,.@x1,.@y1);
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		for (.@i = 0; .@i < $@partymembercount; .@i++) {
			if (isloggedin($@partymemberaid[.@i], $@partymembercid[.@i])) {
				set .@Mission_Count, getvar(#Mission_Count, $@partymembercid[.@i]);
				set .@Mission0, getvar(Mission0, $@partymembercid[.@i]);
				set .@HP, readparam(HP, $@partymembercid[.@i]);

				if (.@Mission_Count && .@Mission0 && .@HP > 0) {
					getmapxy(.@map2$,.@x2,.@y2,BL_PC,rid2name($@partymemberaid[.@i]));
					if ((.@map1$ == .@map2$ || .Party == 1) && (distance(.@x1,.@y1,.@x2,.@y2) <= 30 || .Party < 3)) {
						for (.@j = 0; .@j < .Quests; .@j++) {
							.@my_mob_id = getvar( getd("ภารกิจ"+.@j),$@partymembercid[.@i] );
							.@my_count = getvar( getd("ภารกิจ"+.@j+"_"), $@partymembercid[.@i] );
							if (strmobinfo(1,.@mob) == strmobinfo(1,.@my_mob_id)) {
								if (.@my_count < .@Mission_Count) {
									setd "ภารกิจ"+.@j+"_", (.@my_count+1), $@partymembercid[.@i];
									dispbottom "[ภารกิจนักล่ามอนเตอร์] Killed " + (.@my_count+1) + " of " + .@Mission_Count + " " + strmobinfo(1,.@mob) + ".", 0x777777, $@partymembercid[.@i];
									break;
								}
							}
						}
					}
				}
			}
		}
	}
	end;

OnInit:
//Waitingroom "ภารกิจนักล่ามอนเตอร์" , 0;
	.Delay = 12;            // Quest ล่าช้าเป็นชั่วโมง (0 ถึงปิดใช้งาน)
	.Quests = 4;            // จำนวนการทำเควสย่อยต่อภารกิจ (เพิ่มรางวัล)
	.Party = 3;             // ตัวเลือกปาร์ตี้: 0 (ไม่รวมการสังหารปาร์ตี้), 1 (รวมถึงการสังหารปาร์ตี้), 2 (แผนที่เดียวกันเท่านั้น), 3 (พื้นที่หน้าจอเท่านั้น)
	.Reset = -1;            // ตัวเลือกการรีเซ็ต: -1 (การละทิ้งภารกิจตั้งค่าเวลาหน่วง), 0 (ไม่มีเวลาหน่วง), [Zeny] (ค่าใช้จ่ายในการละทิ้งภารกิจไม่มีเวลาหน่วง)
	setarray .Count[0],     // สัตว์ประหลาดขนาดเล็กและสูงสุดต่อหนึ่งข้อความย่อย (เพิ่มรางวัล)
		40,70;
	setarray .Modifier[0],  // ตัวคูณสำหรับค่าประสบการณ์พื้นฐาน, ประสบการณ์งานและรางวัล Zeny
		getbattleflag("base_exp_rate")/100,getbattleflag("job_exp_rate")/100,60;
	.mob_db$ =              // ชื่อตารางของฐานข้อมูล SQL mob mob
		(checkre(0))?"mob_db_re":"mob_db";
	setarray .Shop[0],      // รายการรางวัล: <ID>, <จุดราคา> (ประมาณ 10 ~ 20 คะแนนต่อการค้นหา)
		512,1,513,1,514,1,538,5,539,5,558,10,561,10;
	.Blacklist$ =           // รหัสม็อบที่ขึ้นบัญชีดำ
		"1062,1088,1183,1186,1200,1212,1220,1221,1234,1235,"+
		"1244,1245,1250,1268,1290,1293,1294,1296,1298,1299,"+
		"1300,1301,1303,1304,1305,1306,1308,1309,1311,1313,"+
		"1515,1588,1618,1676,1677,1678,1679,1796,1797,1974,"+
		"1975,1976,1977,1978,1979";

	npcshopdelitem "mission_shop",512;
	for (.@i = 0; .@i < getarraysize(.Shop); .@i += 2)
		npcshopadditem "mission_shop", .Shop[.@i], .Shop[.@i+1];
	end;
}
-	shop	mission_shop	-1,512:-1
