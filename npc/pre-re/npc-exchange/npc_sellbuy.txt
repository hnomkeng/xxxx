/* การตั้งค่า เพิ่มไอเท็ม ราคาไอเท็ม



	setarray .Shop[0],501,10, ไอเท็ม 501 =  RedPotins , 10 = จำนวนราคา (ใช้เป็นหน่วย Points)

					  502,10,

					  503,10,

					  504,10,

					  505,10; 



*/



/* การตั้งค่า เปลี่ยนแปลง ราคา ต่อการ์ด 1 ใบ 



	setarray .Points[0],10,150;	 หน่วยแรก 10 (จำนวนราคาที่ขายให้กับ Npc)= คือการ์ดแบบปกติ มอนส์เตอร์ทั่วไป / หน่วยสอง 150 (จำนวนราคาที่ขายให้กับ Npc)= คือการ์ดแบบบอสมอนส์เตอร์

	

*/



/* การตั้งค่าการเพิ่ม รหัสไอเท็ม สำหรับการ์ดบอส เป็นเงื่อนใข



	set .MvPCards$,	ใส่เพียงแค่รหัสการ์ดบอส

		"4121,4123,4128,4131,4132,4134,4135,4137,4143,4144,4145,4146,4147,4148,4168,4236,"+

		"4241,4263,4276,4302,4305,4318,4324,4330,4342,4357,4359,4361,4363,4365,4399,4403,4407";



*/



morocc,154,125,5	script	รับซื้อการ์ดขยะ	503,{



	mes "[ Card Trader ]";

	mes "สวัสดี, ^FF0000"+strcharinfo(0)+"^000000!";
	mes "คุณมีแต้มสะสม ( ^0055FF"+getd(.Points$)+"^000000 )";
	next;
	switch(select("^339900->^000000 รายละเอียดข้อมูล:^339900->^000000 ขายการ์ด :^339900->^000000 เข้าสู่ร้านค้า :^FF0000->^000000 ยกเลิก")) {
	case 1:

		mes "[ รายละเอียดข้อมูล ]";

		mes "ถ้าหากคุณมีไอเท็มประเภทการ์ด";

		mes "ที่ไม่ได้ใช้ สามารถขายให้ฉันได้";

		next;

		mes "[ Card Trader ]";

		mes "^0055FF"+.Points[0]+" Point"+((.Points[0] == 1)?"":"")+"^000000 สำหรับ การ์ดปกติ";

		mes "^0055FF"+.Points[1]+" Points^000000 สำหรับ การ์ดบอส.";

		mes "และ คุณสามารถนำ Point มาแลกเปลี่ยนสิ่งของกับเราได้";

		emotion e_cash;

		close;

	case 2:

		mes "[ Card Trader ]";

		mes "กรุณาเลือการ์ดของคุณ";

		mes "ที่จะต้องการแลกเปลี่ยน.";

		deletearray @sold_nameid[0],getarraysize(@sold_nameid);

		callshop "Point_Shops",2;

		npcshopattach "Point_Shops";

		end;

	case 3:

		mes "[ แลกเปลี่ยนสินค้า ]";

		mes "คุณมี ^0055FF"+getd(.Points$)+"^000000 Point"+((getd(.Points$) == 1)?".":"s.");

		callshop "Point_Shops",1;

		npcshopattach "Point_Shops";

		end;

	case 4:

		mes "[ แลกเปลี่ยนการ์ด ]";

		mes "ไว้พบกันใหม่!";

		emotion e_yawn;

		close;		

	}
OnSellItem:

	

	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)

		if (@sold_nameid[.@i] > 4000 && @sold_nameid[.@i] < 4700)

		{

			set .@card_id[getarraysize(.@card_id)], @sold_nameid[.@i];

			set .@card_amt[getarraysize(.@card_amt)], @sold_quantity[.@i];

			set .@mvp, compare(.MvPCards$,""+@sold_nameid[.@i]);

			mes ((.@mvp)?"  ^FF0000":"  ^777777")+getitemname(@sold_nameid[.@i])+" จำนวน "+@sold_quantity[.@i]+" ใบ^000000";

			set .@card_total, .@card_total+(@sold_quantity[.@i]*((.@mvp)?.Points[1]:.Points[0]));

		}

	deletearray @sold_nameid[0], getarraysize(@sold_nameid);

	deletearray @sold_quantity[0], getarraysize(@sold_quantity);

	if (!.@card_id)

	{

		mes "[ Card Trader ]";

		mes "รายการไอเท็มที่แลกเปลี่ยนมันไม่ใช่การ์ด";

		mes "กรุณาทำรายการใหม่อีกครั้ง";

		emotion e_swt;

		close;

	}

	mes " ";

	mes "ราคาทั้งหมด คือ : ^0055FF"+.@card_total+" Point.^000000";

	mes "คุณแน่ใจที่จะต้องการขาย ?";

	next;

	if(select("^339900->^000000 ใช่ ข้าต้องการขาย.:^FF0000->^000000 ยกเลิก") == 2)

	{

		mes "[ Card Trader ]";

		mes "อื้มมม, โอเค ไว้ต้องการเมื่อไรค่อยกลับมาอีกครั้ง...";

		emotion e_hmm;

		close;

	}

	for(set .@i,0; .@i<getarraysize(.@card_id); set .@i,.@i+1)

		delitem .@card_id[.@i],.@card_amt[.@i];

	setd .Points$, getd(.Points$)+.@card_total;

	mes "[ Card Trader ]";

	mes "แลกเปลี่ยนสำเร็จ !";

	mes "ขอบคุณสำหรับการซื้อขาย แลกเปลี่ยน";

	emotion e_ho;

	close;



OnBuyItem:

	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)

		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)

			if (@bought_nameid[.@i] == .Shop[.@j])

			{

				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);

				break;

			}

	if (.@cost > getd(.Points$))

	{

		mes "[ แลกเปลี่ยนสินค้า ]";

		mes "คุณมีจำนวน Points ไม่เพียงพอต่อการแลกเปลี่ยน.";

		emotion e_omg;

	}

	else {

		mes "[ แลกเปลี่ยนสินค้า ]";

		mes "	";

		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)

		{

			getitem @bought_nameid[.@i], @bought_quantity[.@i];

			mes "  ^777777"+getitemname(@bought_nameid[.@i])+" จำนวน "+@bought_quantity[.@i]+" อัน^000000";

		}

		mes " ";

		mes "ราคาทั้งหมด คือ : ^0055FF"+.@cost+" pt.^000000";

		setd .Points$, getd(.Points$)-.@cost;

		emotion e_cash;

	}

	deletearray @bought_nameid[0], getarraysize(@bought_nameid);

	deletearray @bought_quantity[0], getarraysize(@bought_quantity);

	close;



OnInit:



	set .Points$,"#Card_Points";	// ค่าตัวแปร เก็บคะแนนพอยท์ (ห้ามแก้ใขมีผลต่อ Base ที่เก็บค่าบน SQL)

	setarray .Shop[0],616,20,4206,60,4205,120,4158,150,4279,200,4140,300,12075,10,12080,10,12085,10,12090,10,12095,10,12100,10;		// ตั้งค่า เพิ่ม / ลบ / แก้ใข รายการไอเท็มที่ใช้การซื้อขาย : <รหัสไอเท็ม>,<จำนวนราคา>



	setarray .Points[0],1,100;		// ตั้งค่า ราคาการขายการ์ดให้กับ Npc <การ์ดปกติ>,<การ์ด บอส>

	set .MvPCards$,					// ตั้งค่า รหัสไอเท็ม การ์ด สำหรับการ์ดบอส. (ราคาปัจจุบันสำหรับการ์ดบอส ต่อ 1 ใบ คือ 150 ที่จะได้รับเมื่อขาย)

	  "4121,4123,4128,4131,4132,4134,4135,4137,4143,4144,4145,4146,4147,4148,4168,4236,"+

	  "4241,4263,4276,4302,4305,4318,4324,4330,4342,4357,4359,4361,4363,4365,4399,4403,4407";



	npcshopdelitem "Point_Shops",909;

	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)

		npcshopadditem "Point_Shops",.Shop[.@i],.Shop[.@i+1];

		waitingroom "Card Recycle",0;
	end;

}



-	shop	Point_Shops	-1,909:-1