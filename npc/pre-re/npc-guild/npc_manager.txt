-	script	Castle_Manager	-1,{
	.@n$ = "[ Castle Manager ]";
	mes .@n$;
	
	if ( getgmlevel() < 99 ) {
		mes "ขออภัยคุณไม่ใช่ GM!";
		mes "จึงไม่สามารถใช้งานได้ค่ะ.";
		end;
	}
	
	if ( agitcheck() || agitcheck2() ) {
		mes "ไม่สามารถใช้งานขณะกิลด์วอร์ได้.";
		end;
	}
	
	for(.@i = 0; .@i < 6; .@i++) {
		.@k = .@i * 5;
		mes "ข้อมูลปราสาท: ^FF0000"+.Realms$[.@i]+"^000000";
		for(.@j = .@k; .@j < (.@k + 5); .@j++) {
			.@t = getcastledata(.Castles$[.@j],1);
			mes " "+getcastlename(.Castles$[.@j])+": "+((.@t)?"^0055FF"+getguildname(.@t):"^777777ไม่มีผู้ครอบครอง")+"^000000";
		}
		if (.@i < 5) mes " ";
	}
	
	next;
	
	switch(select("^339900->^000000 เคลียบ้านกิลด์วอร์:^339900->^000000 แก้ไขบ้านกิลด์วอร์:^FF0000->^000000 ยกเลิก")){
		case 1:
			mes .@n$,"ท่านต้องการเคลียข้อมูลบ้านใด ?"; next;
			
			for(.@i = 0; .@i < 6; .@i++) {
				.@k = .@i * 5;
				for(.@j = .@k; .@j < (.@k + 5); .@j++) {
					.@t = getcastledata(.Castles$[.@j],1);
					if(.@t) {
						.@menu1$ = .@menu1$ + "  "+getcastlename(.Castles$[.@j])+" (^0055FF"+.Castles$[.@j]+"^000000):";
						.@menuIds1[.@id1] = .@j;
						.@id1++;
					}
					else {
						.@menu2$ = .@menu2$ + "  "+getcastlename(.Castles$[.@j])+" (^777777"+.Castles$[.@j]+"^000000):";
						.@menuIds2[.@id2] = .@j;
						.@id2++;
					}
				}
			}
			
			copyarray(.@menuIds1[.@id1], .@menuIds2[0], .@id2);
			
			.@clear = select(.@menu1$ + .@menu2$) - 1;
			.@clear = .@menuIds1[.@clear];
			
			mes .@n$,"ท่านต้องการเคลียข้อมูลบ้าน ^0055FF"+getcastlename(.Castles$[.@clear])+"^000000's ใช่ไหม ?"; next;
			
			switch(select("^339900->^000000 ใช่! ข้าต้องการเคลียมัน.:^FF0000->^000000 ยกเลิก")){
				case 1:
					for (.@i = 1; .@i < 18; .@i++){
						setcastledata(.Castles$[.@clear],.@i, 0);
					}
					mes .@n$,"^0055FF"+getcastlename(.Castles$[.@clear])+"^000000's เคลียสำเร็จแล้ว.";
					break;
				case 2:
					break;
			}
			break;
		case 2:
			mes .@n$,"ท่านต้องการแก้ไขข้อมูลบ้านใด ?"; next;
			
			for(.@i = 0; .@i < 6; .@i++) {
				.@k = .@i * 5;
				for(.@j = .@k; .@j < (.@k + 5); .@j++) {
					.@t = getcastledata(.Castles$[.@j],1);
					if(.@t) {
						.@menu1$ = .@menu1$ + "  "+getcastlename(.Castles$[.@j])+" (^0055FF"+.Castles$[.@j]+"^000000):";
						.@menuIds1[.@id1] = .@j;
						.@id1++;
					}
					else {
						.@menu2$ = .@menu2$ + "  "+getcastlename(.Castles$[.@j])+" (^777777"+.Castles$[.@j]+"^000000):";
						.@menuIds2[.@id2] = .@j;
						.@id2++;
					}
				}
			}
			
			copyarray(.@menuIds1[.@id1], .@menuIds2[0], .@id2);
			
			.@old = select(.@menu1$ + .@menu2$) - 1;
			.@old = .@menuIds1[.@old];
			
			mes .@n$,"ท่านต้องการแก้ไขบ้าน ^0055FF"+getcastlename(.Castles$[.@old])+"^000000 ในส่วนไหนล่ะ ?"; next;
			
			.@change = select(.Values$[1],.Values$[2],.Values$[3],.Values$[4],.Values$[5]);
			
			mes .@n$,"กรุณากรอกข้อมูลการแก้ไข ^0055FF"+.Values$[.@change]+"^000000?"; next;
			
			input(.@n);
			setcastledata(.Castles$[.@old],.@change, .@n);
			
			mes .@n$,"^0055FF"+getcastlename(.Castles$[.@old])+"^000000's ^0055FF"+.Values$[.@change]+"^000000 ได้ถูกแก้ไขเป็น "+.@n;
			break;
		case 3:
			break;
	}
	end;

OnInit:
	setarray .Values$[1],"Guild ID","Economy","Defense","Economy added today","Defense added today";
	setarray .Castles$[0],
	"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
	"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
	"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
	"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",
	"arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",
	"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05";
	setarray .Realms$[0],"Prontera","Payon","Geffen","Aldebaran","Arunafeltz","Schwaltzvalt";
waitingroom "-> จัดการระบบวอร์", 0;
	end;
}

oasis,160,172,5	duplicate(Castle_Manager)	Castle Manager#war_all	895